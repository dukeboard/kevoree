# KevScript grammar

# Rules
# =====
KevScript   <- *(Statement ws)

Statement   <- (Add | Remove | Move | Attach | Detach | Set | AddBinding | Merge)

Add         <- AddToken ws string ws ':' ws string                                 # add node0 : JavaSENode
             | AddToken ws NameList ws ':' ws string                               # add node42, node1 : JavascriptNode

Remove      <- RemoveToken ws string                                               # remove node0
             | RemoveToken ws NameList ws ':' ws string                            # remove chan1, node0

Move        <- MoveToken ws string ws string                                       # move node0 group1
             | MoveToken ws NameList ws string                                     # move node0, node1 group1

Attach      <- AttachToken ws string ws string                                     # attach node0 group0
             | AttachToken ws NameList ws string                                   # attach node0, node1 group0

Detach      <- DetachToken ws string ws string                                     # detach node0 group0
             | DetachToken ws NameList ws string                                   # detach node0, node1 group0

Set         <- SetToken ws string ws Dictionary                                    # set comp0 {}

Dictionary  <- '{' ws AttrList ws '}'                                              # { Attribute, Attribute, ... }

AttrList    <- Attribute ws *([,] ws string)                                       # Attribute, Attribute, Attribute, ...

Attribute   <- string ws [=:] ws ['"] ws string ws ['"]                            # myAttr = ['"] foo ['"]

AddBinding  <- string'.'string ws '=>' ws string                                   # comp0.sendMsg => chan42

Merge       <- MergeToken ws MergeDef                                              # merge MergeDef

MergeDef    <- string ws ':' ws stringdot ws ':' ws stringdot ws (':'|'@') ws stringdot # mvn:groupId:artId[:@]version
             | string ws ':' ws string ws (':'|'@') ws string                      # npm:moduleName[:@]version

NameList    <- string ws *([,] ws string)                                          # node0, node42, node1024

stringdot   <- +(char | '.')                                                       # String with dot allowed

string      <- +char                                                               # String without dots
# =========
# End Rules


# Non-terminals
# =============
MergeToken      <: 'merge'
AddToken        <: 'add'
RemoveToken     <: 'remove'
MoveToken       <: 'move'
SetToken        <: 'set'
AttachToken     <: 'attach'
DetachToken     <: 'detach'
char            <: [a-zA-Z0-9_-]
ws              <: *[ \t\n\r]
# =================
# End Non-terminals
