# KevScript grammar

# Rules
# =====
KevScript   <- +(Statement ws)

Statement   <- (Add | Remove | Move | Attach | Detach | Set | AddBinding | Merge) ws

Add         <- AddToken ws String ws :':' ws String                                  # add node0 : JavaSENode
             | AddToken ws NameList ws :':' ws String                                # add node42, node1 : JavascriptNode

Remove      <- RemoveToken ws String                                                 # remove node0
             | RemoveToken ws NameList ws :':' ws String                             # remove chan1, node0

Move        <- MoveToken ws String ws String                                         # move node0 group1
             | MoveToken ws NameList ws String                                       # move node0, node1 group1

Attach      <- AttachToken ws String ws String                                       # attach node0 group0
             | AttachToken ws NameList ws String                                     # attach node0, node1 group0

Detach      <- DetachToken ws String ws String                                       # detach node0 group0
             | DetachToken ws NameList ws String                                     # detach node0, node1 group0

Set         <- SetToken ws String ws Dictionary                                      # set comp0 {}

Dictionary  <- :'{' ws AttrList ws :'}'                                              # { Attribute, Attribute, ... }

AttrList    <- Attribute ws *(:[,] ws String)                                        # Attribute, Attribute, Attribute, ...

Attribute   <- String ws [=:] ws ['"] ws String ws ['"]                              # myAttr = ['"] foo ['"]

AddBinding  <- String :'.' String ws :'=>' ws String                                 # comp0.sendMsg => chan42

Merge       <- MergeToken ws MergeDef                                                # merge MergeDef

MergeDef    <- String ws :':' ws String2 ws :':' ws String2 ws :(':'|'@') ws String2 # mvn:groupId:artId[:@]version
             | String ws :':' ws String ws :(':'|'@') ws String2                     # npm:moduleName[:@]version

NameList    <- String ws *(:[,] ws String)                                           # node0, node42, node1024
# =========
# End Rules


# Void Non-terminals
# =============
MergeToken      <- 'merge'
AddToken        <- 'add'
RemoveToken     <- 'remove'
MoveToken       <- 'move'
SetToken        <- 'set'
AttachToken     <- 'attach'
DetachToken     <- 'detach'
String          <- +[a-zA-Z0-9_-]
String2         <- +[a-zA-Z0-9_-.]
eol             <: '\r\n' | '\n' | '\r'
ws              <: *([ \t] | eol)
# =================
# End Void Non-terminals