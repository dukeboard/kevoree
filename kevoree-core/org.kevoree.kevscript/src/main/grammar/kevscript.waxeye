# KevScript grammar

# Rules
# =====
KevScript    <- +((Statement | :Comment eol) ws) :?Comment

Statement    <- Add | Start | Stop | Remove | Move | Attach | Detach | Set | AddBinding | DelBinding | Include | Network | AddRepo | Namespace

Add          <- AddToken ws NameList ws :':' ws TypeDef                                  # add group0, group1 : WebSocketGroup

Start        <- StartToken ws NameList                                                   # start group1, node0, node1

Stop         <- StopToken ws NameList                                                     # stop group1, node0, node1

Remove       <- RemoveToken ws NameList                                                  # remove node0, node0.comp1, sync

Move         <- MoveToken ws NameList ws InstancePath                                    # move node0.comp0, node2.* node1

Attach       <- AttachToken ws NameList ws InstancePath                                  # attach node0, node1 group0

Detach       <- DetachToken ws NameList ws InstancePath                                  # detach node0, node1 group0

Set          <- SetToken ws InstancePath ?(:'/' InstancePath) ws :'=' ws RealString      # set node0.comp0.myAtt = 'foo'
                                                                                         # set sync.myAtt/node0 = "foo"

Network      <- NetworkToken ws InstancePath ws String2                                  # network node1.lan.eth0 192.168.0.1

AddBinding   <- BindToken ws InstancePath ws InstancePath                                # bind node1.comp0.sendMsg chan42

DelBinding   <- UnbindToken ws InstancePath ws InstancePath                              # unbind node1.comp0.sendMsg chan0

AddRepo      <- RepoToken ws RealString                                                  # repo "http://org.sonatype.org/foo/bar?a=b&c=d"

Include      <- IncludeToken ws String :':' String2                                      # include npm:kevoree-chan-websocket
																					     # include mvn:org.kevoree.library.javase:websocketgrp:2.0.5-SNAPSHOT

NameList     <- InstancePath ws *(:[,] ws InstancePath)                                  # node42
                                                                                         # node0, node0.comp1, node42

TypeDef      <- String ?(:'/' Version)                                                   # FooType/0.0.1 (specific vers.)
                                                                                         # FooType       (last vers.)

Namespace    <- NamespaceToken ws String                                                 # namespace sp-ace_0

InstancePath <- (Wildcard | String) *(:[.] (Wildcard | String))                          # node0.*.att

Wildcard     <- '*'

String       <- +[a-zA-Z0-9_-]

String2      <- +[a-zA-Z0-9.:@_-]

#parameterValue <- +(!["'] .)

Version      <- +[a-zA-Z0-9._-]

RepoString   <- +(!eol :![ ] .)															# anything but EOL and ' '

Anything     <- +(!eol .) 																# anything but EOL

Escaped      <- ['] | ["] | '\\'

RealString  <- :["] *( :'\\' Escaped | !Escaped . ) :["] | :['] *( :'\\' Escaped | !Escaped . ) :[']

# =========
# End Rules


# Void Non-terminals
# =============
RepoToken       <: 'repo'
IncludeToken    <: 'include'
AddToken        <: 'add'
StartToken      <: 'start'
StopToken       <: 'stop'
RemoveToken     <: 'remove'
MoveToken       <: 'move'
SetToken        <: 'set'
AttachToken     <: 'attach'
DetachToken     <: 'detach'
NetworkToken    <: 'network'
BindToken       <: 'bind'
UnbindToken     <: 'unbind'
NamespaceToken  <: 'namespace'
Comment         <: '//' Anything
eol             <: '\r\n' | '\n' | '\r'
ws              <: *([ \t] | eol)
# =================
# End Void Non-terminals